function(postpend var suffix)
 set(listVar "")
 foreach(f ${ARGN})
   list(APPEND listVar "${f}${suffix}")
 endforeach(f)
 set(${var} "${listVar}" PARENT_SCOPE)
endfunction(postpend)
 
function(vanga_add_library NAME)

  set(options)
  set(one_value_args)
  set(multi_value_args
    SOURCES
    PUBLIC_LINK_LIBRARIES
    PRIVATE_LINK_LIBRARIES
    VANGA_PUBLIC_LINK_LIBRARIES
    VANGA_PRIVATE_LINK_LIBRARIES
    )
  cmake_parse_arguments(VANGA_ADD_LIBRARY "${options}" "${one_value_args}" "${multi_value_args}" ${ARGN})

  add_library("${NAME}_obj" OBJECT ${VANGA_ADD_LIBRARY_SOURCES})
  set_property(TARGET "${NAME}_obj" PROPERTY POSITION_INDEPENDENT_CODE 1)

  message(STATUS "vanga_add_library(${NAME}): SOURCES = ${VANGA_ADD_LIBRARY_SOURCES}, PUBLIC_LINK_LIBRARIES = ${VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES}, PRIVATE_LINK_LIBRARIES = ${VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES}")
  message(STATUS "vanga_add_library(${NAME}): VANGA_BUILD_SHARED_LIBS = ${VANGA_BUILD_SHARED_LIBS}, VANGA_BUILD_STATIC_LIBS = ${VANGA_BUILD_STATIC_LIBS}")

  if (VANGA_BUILD_SHARED_LIBS)
    add_library(${NAME} SHARED $<TARGET_OBJECTS:${NAME}_obj>)

    message(STATUS "vanga_add_library(${NAME}), shared: link ${VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES} ${VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES}")

    target_link_libraries(${NAME}
      PUBLIC
        ${VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES}
        ${VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES}
      )
  endif()

  if (VANGA_BUILD_STATIC_LIBS)
    add_library(${NAME}_static STATIC $<TARGET_OBJECTS:${NAME}_obj>)

    postpend(TRANSFORMED_VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES "_static" ${VANGA_ADD_LIBRARY_VANGA_PUBLIC_LINK_LIBRARIES})
    postpend(TRANSFORMED_VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES "_static" ${VANGA_ADD_LIBRARY_VANGA_PRIVATE_LINK_LIBRARIES})

    #message(STATUS "TRANSFORMED_VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES: ${TRANSFORMED_VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES}")
    #message(STATUS "TRANSFORMED_VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES: ${TRANSFORMED_VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES}")

    #message(STATUS "vanga_add_library(${NAME}): ${TRANSFORMED_VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES}")
    message(STATUS "vanga_add_library(${NAME}), static: link public ${VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES} / ${TRANSFORMED_VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES}")
    message(STATUS "vanga_add_library(${NAME}), static: link private ${VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES} / ${TRANSFORMED_VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES}")

    target_link_libraries(${NAME}_static
      PUBLIC
        ${VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES}
        ${TRANSFORMED_VANGA_ADD_LIBRARY_PUBLIC_LINK_LIBRARIES}
      PRIVATE
        ${VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES}
        ${TRANSFORMED_VANGA_ADD_LIBRARY_PRIVATE_LINK_LIBRARIES}
      )
  endif()

endfunction()
